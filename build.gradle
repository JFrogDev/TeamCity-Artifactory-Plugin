apply plugin: 'idea'


final buildInfoVersion = '2.0.x-SNAPSHOT'


buildscript {
    repositories {
        mavenLocal()
        mavenRepo url: 'http://evgenyg.artifactoryonline.com/evgenyg/repo/'
    }
    dependencies {
        classpath 'com.github.goldin.plugins:teamcity:0.1.4-SNAPSHOT'
    }
}


apply from: 'https://raw.github.com/evgeny-goldin/gradle-plugins/master/teamcity/teamcity.gradle'


defaultTasks 'clean', 'assembleTeamcityPlugin', 'build', 'install'


idea.project {
    jdkName = '1.6'
    ipr.withXml { provider -> provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git' }
}


subprojects {

    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'maven'

    sourceCompatibility = 1.5
    targetCompatibility = 1.5
    group               = 'org.jfrog.teamcity'


    repositories {
        mavenCentral()
        mavenRepo url: 'http://repo.jfrog.org/artifactory/repo/'
    }


    idea.module {
        downloadSources = true
        downloadJavadoc = false
    }

    dependencies {
        testCompile 'org.testng:testng:6.3',
                    'org.easymock:easymock:3.0'
    }


    clean { delete( "$rootDir/out", "$rootDir/build" ) }


    task sourcesJar( type: Jar, dependsOn: classes ) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }


    task javadocJar( type: Jar, dependsOn: javadoc ) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }


    artifacts {
         archives sourcesJar
         archives javadocJar
    }
}


project( ':common' ) {

    dependencies {
        compile ( "org.jfrog.buildinfo:build-info-api:$buildInfoVersion" ){
            exclude group: 'com.google.guava'
        }
        compile "org.jfrog.buildinfo:build-info-client:$buildInfoVersion",
                'com.google.guava:guava:10.0.1',
                'commons-io:commons-io:2.1',
                'org.apache.commons:commons-lang3:3.0.1'
    }
}


project( ':agent' ) {

    configurations { compile { extendsFrom teamcityAgentEx }}

    dependencies   {
        compile project( ':common' ),
                'org.apache.httpcomponents:httpclient:4.1.2',
                "org.jfrog.buildinfo:build-info-vcs:$buildInfoVersion",
                "org.jfrog.buildinfo:build-info-extractor:$buildInfoVersion",
                "org.jfrog.buildinfo:build-info-extractor-ivy:$buildInfoVersion",
                "org.jfrog.buildinfo:build-info-extractor-gradle:$buildInfoVersion",
                "org.jfrog.buildinfo:build-info-extractor-maven3:$buildInfoVersion",
                'org.tmatesoft.svnkit:svnkit:1.3.6-v1',
                'org.apache.commons:commons-compress:1.3'
    }
}


project( ':server' ) {

    configurations { compile { extendsFrom teamcityServerEx }}

    dependencies   {
        compile project( ':common' ),
                "org.jfrog.buildinfo:build-info-extractor:$buildInfoVersion",
                "org.jfrog.buildinfo:build-info-extractor-maven3:$buildInfoVersion"
    }
}


assembleTeamcityPlugin.dependsOn ':agent:jar', ':server:jar'

assembleTeamcityPluginConfig {

    name        'artifactory'
    displayName 'Artifactory'
    description 'Provides seamless integration between Artifactory and TeamCity to support fully traceable and reproducible build artifacts management.'
    downloadUrl "http://repo.jfrog.org/artifactory/libs-releases-local/org/jfrog/teamcity/teamcity-artifactory-plugin/${ project.version }/teamcity-artifactory-plugin-${ project.version }.zip"
    email       'teamcity@jfrog.org'
    vendorName  'JFrog Ltd.'
    vendorUrl   'http://wiki.jfrog.org/confluence/display/RTF/TeamCity+Artifactory+Plug-in'
    vendorLogo  'http://www.jfrog.org/images/logos/jfrog.logo.teamcity.png'
    resources   files( 'Third-Parties-Usage.html' )

    server      project( ':server' )
    agent       project( ':agent'  )
    artifacts   project( ':server' )
}


task wrapper( type: Wrapper ) { gradleVersion = '1.1' }
